// https://medium.com/geekculture/publish-an-android-library-to-github-packages-3c454f6c5d48
//
// To use this library, the user could add the repository:
//
//   maven {
//       url "https://maven.pkg.github.com/wzhijiang"
//   }

apply plugin: "maven-publish"

def githubProperties = new Properties()
rootProject.file("github.properties").withInputStream {
    stream -> githubProperties.load(stream)
}

afterEvaluate {
    publishing {
        repositories {
            maven {
                name = "android-surface"
                url = uri("https://maven.pkg.github.com/wzhijiang/android-surface")
                credentials {
                    username = githubProperties["GITHUB_USERNAME"]
                    password = githubProperties["GITHUB_TOKEN"]
                }
            }
        }
        publications {
            release(MavenPublication) {
                from components.release

                // Replace with your own group ID.
                groupId 'io.github.wzhijiang'

                // Replace with the name of your library
                artifactId 'android-surface'
                version android.defaultConfig.versionName
            }
            debug(MavenPublication) {
                from components.debug

                // Replace with your own group ID.
                groupId 'io.github.wzhijiang'

                // Replace with the name of your library
                artifactId 'android-surface'
                version android.defaultConfig.versionName
            }
        }
    }
}